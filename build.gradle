plugins {
    id 'application'
    id 'checkstyle'
}
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}
application {
    mainClass = 'github.workflow.kata.GOL'
    applicationName = 'gol'
}
version = '1.0.0-SNAPSHOT'

repositories {
    jcenter()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'

    testImplementation 'io.cucumber:cucumber-java8:6.9.0'
    testImplementation 'io.cucumber:cucumber-junit-platform-engine:6.9.0'
}


/* Test configuration */
test {
    useJUnitPlatform()
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

tasks.withType(Checkstyle) {
    ignoreFailures true
}

task cucumber() {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', 'features', 'src/test/resources']
            ignoreExitValue = true
        }
    }
}
/* Gradle customization */
tasks.named('wrapper') {
    distributionType = Wrapper.DistributionType.ALL
}